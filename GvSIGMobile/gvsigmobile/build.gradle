apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion '26.0.2'

    buildTypes {
        release {
            minifyEnabled false
            //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def file = output.outputFile
                    def fileName = file.name.replace("gvsigmobile-release.apk", "GvsigMobile_" + android.defaultConfig.versionName + ".apk");
                    output.outputFileName = new File(file.parentFile, fileName)
                }
            }
        }
    }
    signingConfigs {
        config {
            def Properties props = new Properties()
            def propFile = new File('./ant.properties')

            if (propFile.canRead()) {
                props.load(new FileInputStream(propFile))
                if (props != null && props.containsKey('key.store') && props.containsKey('key.store.password') &&
                        props.containsKey('key.alias') && props.containsKey('key.alias.password')) {
                    storeFile = file(props['key.store'])
                    storePassword = props['key.store.password']
                    keyAlias = props['key.alias']
                    keyPassword = props['key.alias.password']
                    println 'ant.properties found in: ' + propFile.absolutePath
                } else {
                    println 'ant.properties found but some entries are missing'
                    android.buildTypes.release.signingConfig = null
                }
            } else {
                println 'ant.properties not found'
                android.buildTypes.release.signingConfig = null
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }


    defaultConfig {
        applicationId "org.gvsig.gvsigmobile"
        minSdkVersion 18
        targetSdkVersion 25
        versionCode 2
        versionName "0.2.0"
        signingConfig signingConfigs.config
        multiDexEnabled true
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:preference-v7:25.3.1'
    testCompile 'junit:junit:4.12'
    compile project(':geopaparazzi_core')
    compile project(':plugin_gvsigol_io')
    implementation project(':geopaparazzi_default_export_plugins')
    implementation project(':geopaparazzi_default_import_plugins')
    implementation project(':geopaparazzi_pdf_export_plugins')
}
